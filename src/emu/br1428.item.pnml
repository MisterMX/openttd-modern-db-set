#define BR_1428_WAGON_LENGTH 4
#define BR_1428_CARGO_CAPACITY 55	

switch(FEAT_TRAINS, SELF, sw_br1428_articulated_parts, extra_callback_info1) {
    BR_1428_WAGON_LENGTH: CB_RESULT_NO_MORE_ARTICULATED_PARTS;
    br1428;
}

switch(FEAT_TRAINS, SELF, sw_br1428_wagon_attach, vehicle_type_id) {
    br1428: CB_RESULT_ATTACH_ALLOW;
    CB_RESULT_ATTACH_DISALLOW;
}

switch(FEAT_TRAINS, SELF, sw_br1428_gr_reversed, position_in_consist_from_end % BR_1428_WAGON_LENGTH) {
    0: gr_br1428_rear;
	1..3: gr_br1428_middle;
	gr_br1428_front;
}

switch(FEAT_TRAINS, SELF, sw_br1428_gr_not_reversed, position_in_consist % BR_1428_WAGON_LENGTH) {
    0: gr_br1428_front;
	1..3: gr_br1428_middle;
	gr_br1428_rear; 
}

switch(FEAT_TRAINS, PARENT, sw_br1428_gr, vehicle_is_reversed) {
    1: sw_br1428_gr_reversed;
    sw_br1428_gr_not_reversed;
}

switch(FEAT_TRAINS, SELF, sw_br1428_ve_reversed_other, position_in_consist % 2) {
    0: visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 1, ENABLE_WAGON_POWER);
    visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 1, DISABLE_WAGON_POWER);
}

switch(FEAT_TRAINS, SELF ,sw_br1428_ve_reversed, position_in_consist) {
    4: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 1, ENABLE_WAGON_POWER);
    sw_br1428_ve_reversed_other;
}

switch(FEAT_TRAINS, SELF, sw_br1428_ve_not_reversed_other, position_in_consist % 2) {
    0: visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 1, ENABLE_WAGON_POWER);
    visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 1, DISABLE_WAGON_POWER);
}

switch(FEAT_TRAINS, SELF, sw_br1428_ve_not_reversed, position_in_consist) {
    4: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 1, ENABLE_WAGON_POWER);
    sw_br1428_ve_not_reversed_other;
}

switch(FEAT_TRAINS, SELF, sw_br1428_ve, vehicle_is_reversed) {
    1: sw_br1428_ve_reversed;
    sw_br1428_ve_not_reversed;
}

item (FEAT_TRAINS, br1428, 1428) {
    property {
        sprite_id: SPRITE_ID_NEW_TRAIN;
        name: string(STR_BR1428_NAME);
        climates_available: ALL_CLIMATES;
        introduction_date: date(2007, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        loading_speed: 18;
        speed: 161 km/h;
        cost_factor: 85;
        vehicle_life: 30;
        dual_headed: 0;
        running_cost_factor: 170;
        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        track_type: RAIL;
        power: 3000 kW;
        cargo_capacity: BR_1428_CARGO_CAPACITY;
        length:	8;
        weight: 145 tons;
        tractive_effort_coefficient: 0.15;
        cargo_allow_refit: [];
    }
    graphics {
        articulated_part: sw_br1428_articulated_parts;
        default: sw_br1428_gr;
        purchase: gr_br1428_purchase;
        can_attach_wagon: sw_br1428_wagon_attach;
        visual_effect_and_powered: sw_br1428_ve;
        additional_text: string(STR_BR1428_ADDITIONAL_TEXT);
        cargo_capacity: BR_1428_CARGO_CAPACITY;
    }
}

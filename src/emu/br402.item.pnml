#define BR_402_CARGO_CAPACITY 47
#define BR_402_WAGON_LENGTH 8

switch(FEAT_TRAINS, SELF, sw_br402_articulated_parts, extra_callback_info1) {
    BR_402_WAGON_LENGTH: CB_RESULT_NO_MORE_ARTICULATED_PARTS;
    br402;
}

switch(FEAT_TRAINS, SELF, sw_br402_wagon_attach, vehicle_type_id) {
    br402: CB_RESULT_ATTACH_ALLOW;
    CB_RESULT_ATTACH_DISALLOW;
}

switch(FEAT_TRAINS, SELF, sw_br402_gr_reversed, position_in_consist_from_end % BR_402_WAGON_LENGTH) {
    0: gr_br402_end_power_rear;
    1..3: gr_br402_waggon;
    4: gr_br402_restaurant1;
    5..6: gr_br402_waggon;
    gr_br402_end_front;
}

switch(FEAT_TRAINS, SELF, sw_br402_gr_not_reversed, position_in_consist % BR_402_WAGON_LENGTH) {
    0: gr_br402_end_power_front;
    1..3: gr_br402_waggon;
    4: gr_br402_restaurant2;
    5..6: gr_br402_waggon;
    gr_br402_end_rear;
}

switch(FEAT_TRAINS, PARENT, sw_br402_gr, vehicle_is_reversed) {
    1: sw_br402_gr_reversed;
    sw_br402_gr_not_reversed;
}

switch(FEAT_TRAINS, SELF, sw_br402_ve, position_in_consist % 2) {
    0: visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 1, ENABLE_WAGON_POWER);
    visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 1, DISABLE_WAGON_POWER);
}

item (FEAT_TRAINS, br402, 402) {
    property {
        sprite_id: SPRITE_ID_NEW_TRAIN;
        name: string(STR_NAME_BR402);
        climates_available: ALL_CLIMATES;
        introduction_date: date(1995, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        loading_speed: 18;
        speed: 281 km/h;
        cost_factor: 1;
        vehicle_life: 30;
        dual_headed: 0;
        running_cost_factor: 170;
        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        track_type: RAIL;
        power: 9600 kW;
        cargo_capacity: BR_402_CARGO_CAPACITY;
        length:	8;
        weight: 795 tons;
        tractive_effort_coefficient: 0.15;
        cargo_allow_refit: [];
    }
    graphics {
        articulated_part: sw_br402_articulated_parts;
        default: sw_br402_gr;
        purchase: gr_br402_purchase;
        can_attach_wagon: sw_br402_wagon_attach;
        visual_effect_and_powered: sw_br402_ve;
        additional_text: string(STR_BR402_ADDITIONAL_TEXT);
        cargo_capacity: BR_402_CARGO_CAPACITY;
    }
}

#define BR403_CARGO_CAPACITY 57
#define BR_403_WAGON_LENGTH 8

switch(FEAT_TRAINS, SELF, sw_br403_articulated_parts, extra_callback_info1) {
    BR_403_WAGON_LENGTH: CB_RESULT_NO_MORE_ARTICULATED_PARTS;
    br403;
}

switch(FEAT_TRAINS, SELF, sw_br403_wagon_attach, vehicle_type_id) {
    br403: CB_RESULT_ATTACH_ALLOW;
    br407: CB_RESULT_ATTACH_ALLOW;
    CB_RESULT_ATTACH_DISALLOW;
}

switch(FEAT_TRAINS, SELF, sw_br403_gr_reversed, position_in_consist_from_end % BR_403_WAGON_LENGTH) {
    0: gr_br403_rear;
    1: gr_br403_waggon2_power;
    2: gr_br403_waggon1;
    3: gr_br403_restaurant1;
    4: gr_br403_waggon2;
    5: gr_br403_waggon2;
    6: gr_br403_waggon1;
    gr_br403_front;
}

switch(FEAT_TRAINS, SELF, sw_br403_gr_not_reversed, position_in_consist % BR_403_WAGON_LENGTH) {
    0: gr_br403_front;
    1: gr_br403_waggon1;
    2: gr_br403_waggon2;
    3: gr_br403_restaurant2;
    4: gr_br403_waggon1;
    5: gr_br403_waggon1;
    6: gr_br403_waggon2_power;
    gr_br403_rear;
}

switch(FEAT_TRAINS, PARENT, sw_br403_gr, vehicle_is_reversed) {
    1: sw_br403_gr_reversed;
    sw_br403_gr_not_reversed;
}

switch(FEAT_TRAINS, SELF, sw_br403_ve_reversed_other, position_in_consist % 2) {
    0: visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 1, ENABLE_WAGON_POWER);
    visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 1, DISABLE_WAGON_POWER);
}

switch(FEAT_TRAINS, SELF ,sw_br403_ve_reversed, position_in_consist) {
    6: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 1, ENABLE_WAGON_POWER);
    sw_br403_ve_reversed_other;
}

switch(FEAT_TRAINS, SELF, sw_br403_ve_not_reversed_other, position_in_consist % 2) {
    0: visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 1, ENABLE_WAGON_POWER);
    visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 1, DISABLE_WAGON_POWER);
}

switch(FEAT_TRAINS, SELF, sw_br403_ve_not_reversed, position_in_consist) {
    6: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 1, ENABLE_WAGON_POWER);
    sw_br403_ve_not_reversed_other;
}

switch(FEAT_TRAINS, SELF, sw_br403_ve, vehicle_is_reversed) {
    1: sw_br403_ve_reversed;
    sw_br403_ve_not_reversed;
}

item (FEAT_TRAINS, br403, 403) {
    property {
        sprite_id: SPRITE_ID_NEW_TRAIN;
        name: string(STR_BR403_NAME);
        climates_available: ALL_CLIMATES;
        introduction_date: date(2000, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        loading_speed: 18;
        speed: 331 km/h;
        cost_factor: 105;
        vehicle_life: 30;
        dual_headed: 0;
        running_cost_factor: 170;
        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        track_type: RAIL;
        power: 8000 kW;
        cargo_capacity: BR403_CARGO_CAPACITY;
        length:	8;
        weight: 409 tons;
        tractive_effort_coefficient: 0.15;
        cargo_allow_refit: [];
    }
    graphics {
        articulated_part: sw_br403_articulated_parts;
        default: sw_br403_gr;
        purchase: gr_br403_purchase;
        can_attach_wagon: sw_br403_wagon_attach;
        visual_effect_and_powered: sw_br403_ve;
        additional_text: string(STR_BR403_ADDITIONAL_TEXT);
        cargo_capacity: BR403_CARGO_CAPACITY;
    }
}

#define BR415_CARGO_CAPACITY 50
#define BR_415_WAGON_LENGTH 5

switch(FEAT_TRAINS, SELF, sw_br415_articulated_parts, extra_callback_info1) {
    BR_415_WAGON_LENGTH: CB_RESULT_NO_MORE_ARTICULATED_PARTS;
    br415;
}

switch(FEAT_TRAINS, SELF, sw_br415_wagon_attach, vehicle_type_id) {
    br415: CB_RESULT_ATTACH_ALLOW;
    CB_RESULT_ATTACH_DISALLOW;
}

switch(FEAT_TRAINS, SELF, sw_br415_gr_reversed, position_in_consist_from_end % BR_415_WAGON_LENGTH) {
    0: gr_br411_rear;
    1: gr_br411_waggon2;
    2: gr_br411_restaurant;
    3: gr_br411_waggon;
    gr_br411_front;
}

switch(FEAT_TRAINS, SELF, sw_br415_gr_not_reversed, position_in_consist % BR_415_WAGON_LENGTH) {
    0: gr_br411_front;
    1: gr_br411_waggon;
    2: gr_br411_restaurant;
    3: gr_br411_waggon2;
    gr_br411_rear;
}

switch(FEAT_TRAINS, PARENT, sw_br415_gr, vehicle_is_reversed) {
    1: sw_br415_gr_reversed;
    sw_br415_gr_not_reversed;
}

switch(FEAT_TRAINS, SELF, sw_br415_ve_reversed_other, position_in_consist % 2) {
    0: visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 1, ENABLE_WAGON_POWER);
    visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 1, DISABLE_WAGON_POWER);
}

switch(FEAT_TRAINS, SELF ,sw_br415_ve_reversed, position_in_consist) {
    4: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 1, ENABLE_WAGON_POWER);
    sw_br415_ve_reversed_other;
}

switch(FEAT_TRAINS, SELF, sw_br415_ve_not_reversed_other, position_in_consist % 2) {
    0: visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 1, ENABLE_WAGON_POWER);
    visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 1, DISABLE_WAGON_POWER);
}

switch(FEAT_TRAINS, SELF, sw_br415_ve_not_reversed, position_in_consist) {
    4: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 1, ENABLE_WAGON_POWER);
    sw_br415_ve_not_reversed_other;
}

switch(FEAT_TRAINS, SELF, sw_br415_ve, vehicle_is_reversed) {
    1: sw_br415_ve_reversed;
    sw_br415_ve_not_reversed;
}

item (FEAT_TRAINS, br415, 415) {
    property {
        sprite_id: SPRITE_ID_NEW_TRAIN;
        name: string(STR_BR415_NAME);
        climates_available: ALL_CLIMATES;
        introduction_date: date(1996, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        loading_speed: 18;
        speed: 230 km/h;
        cost_factor: 140;
        vehicle_life: 30;
        dual_headed: 0;
        running_cost_factor: 170;
        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        track_type: RAIL;
        power: 3000 kW;
        cargo_capacity: BR415_CARGO_CAPACITY;
        length:	8;
        weight: 273 tons;
        tractive_effort_coefficient: 0.15;
        cargo_allow_refit: [];
        misc_flags: bitmask(TRAIN_FLAG_TILT);
    }
    graphics {
        articulated_part: sw_br415_articulated_parts;
        default: sw_br415_gr;
        purchase: gr_br411_purchase;
        can_attach_wagon: sw_br415_wagon_attach;
        visual_effect_and_powered: sw_br415_ve;
        additional_text: string(STR_BR415_ADDITIONAL_TEXT);
        cargo_capacity: BR415_CARGO_CAPACITY;
    }
}
